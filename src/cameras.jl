# Raytracer.jl
# Raytracing for the generation of photorealistic images in Julia
# Copyright (c) 2021 Samuele Colombo, Paolo Galli

# Implementation of the Camera abstract type and the two derivating concrete types,
# OrthogonalCamera and PerspectiveCamera
# TODO write docstrings


"""
    Camera

An abstract type representing an observer.
"""
abstract type Camera end


#####################################################################


"""
    OrthogonalCamera

A camera implementing an orthogonal 3D → 2D projection.

This type implements an observer seeing the world through an orthogonal projection.

Members:
- `aspect_ratio` ([`Real`](@ref)): defines how larger than the height is the image (16/9, 4/3, ...),
you can use julia rational like `16//9`.
- `transformation` ([`Transformation`](@ref)): define the transformation applied to the rays generated by the camera.
"""
Base.@kwdef struct OrthogonalCamera <: Camera
    aspect_ratio::Float32 = 1f0
    transformation::Transformation = Transformation()
end

"""
    fire_ray(camera, u, v)

Fire a [`Ray`](@ref) through the [`Camera`](@ref) at a position (u,v) on the screen.

Parameters `u` and `v` are bound between `0` and `1`:

    (0, 1)                            (1, 1)
        +------------------------------+
        |                              |
        |                              |
        |                              |
        +------------------------------+
    (0, 0)                            (1, 0)

Type parameter `T` is passed onto the [`Ray`](@ref) constructor. Default type is `Float32`.
"""
function fire_ray(camera::OrthogonalCamera, u::Float32, v::Float32)
    camera.transformation * Ray(Point(-1f0, (1f0 - 2u) * camera.aspect_ratio, 2v - 1f0), 
                                VEC_X,
                                tmin = 1f0)
end


#####################################################################


"""
    PerspectiveCamera

A camera implementing a perspective 3D → 2D projection.

This type implements an observer seeing the world through a perspective projection.

Members:
- `aspect_ratio` ([`Real`](@ref)): defines how larger than the height is the image (16/9, 4/3, ...), 
you can use julia rational like `16//9`.
- `transformation` ([`Transformation`](@ref)): define the transformation applied to the rays generated by the camera.
- `screen_distance` ([`Real`](@ref)): tells how much far from the eye of the observer is the screen
and it influences the FOV.
"""
Base.@kwdef struct PerspectiveCamera <: Camera
    aspect_ratio::Float32 = 1f0
    transformation::Transformation = Transformation()
    screen_distance::Float32 = 1f0
end

"""
    fire_ray(camera, u, v)

Fire a [`Ray`](@ref) through the [`Camera`](@ref) at a position (u,v) on the screen.

Parameters `u` and `v` are bound between `0` and `1`:

    (0, 1)                            (1, 1)
        +------------------------------+
        |                              |
        |                              |
        |                              |
        +------------------------------+
    (0, 0)                            (1, 0)

Type parameter `T` is passed onto the [`Ray`](@ref) constructor. Default type is `Float32`.
"""
function fire_ray(camera::PerspectiveCamera, u::Float32, v::Float32)
    camera.transformation * Ray(Point(-camera.screen_distance, 0f0, 0f0),
                                Vec(camera.screen_distance, (1f0 - 2u) * camera.aspect_ratio, 2v - 1f0),
                                tmin = 1f0)
end

################
# Miscellaneous

"""
    aperture_deg(camera)

Compute the FOV of the camera in degrees. `camera` must be a [`PerspectiveCamera`](@ref) instance.
"""
aperture_deg(camera::PerspectiveCamera) = 2f0 * rad2deg(atan(camera.screen_distance, camera.aspect_ratio))
